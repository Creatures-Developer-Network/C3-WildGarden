*GB Template
*# Pray-File "GB_BalloonBug.agent"
*# DS-Name "Balloon Bug"
*# Depend balloonbug.c16
*# Attach gb_c3_conv.c16

*# desc = "C3 Balloon Bug raw, buggy, unsupported conversion."
*# Agent Animation File = "gb_c3_conv.c16"
*# Agent Animation Gallery = "gb_c3_conv"
*# Agent Sprite First Image = 1
*# GB_Category = 3
*# Agent Author = "Creature Labs"

new: simp 2 13 9 "balloonbug" 42 0 3000
attr 198
bhvr 59
clac 0

elas 0
*bioenergy
setv ov61 40
mvsf game "ch_x" game "ch_y"

tick 5

setv ov10 rand 0 1
doif ov10 eq 0
	setv ov10 -1
endi
setv ov11 rand 0 1
doif ov11 eq 0
	setv ov11 -1
endi



**100% copypasted scripts
***balloon bug activate
scrp 2 13 9 1
	doif ov00 eq 0

		setv ov91 100
	endi
	targ from
	doif fmly eq 4
		stim writ targ 85 1
	endi
endm


*****Balloon bug timer
scrp 2 13 9 9
**OV00 is state
*0=in air
*1=on ground
*3=hunting

**OV12 X velocity
**OV13 Y velocity
**OV14 is max X velocity
**OV15 is max Y velocity

**OV90 indecision counter
**OV91 is change activity counter

*increase age
	addv ov01 1
*if old enough
	doif ov01 ge 100
		doif totl 2 13 9 lt 20
			rnge 1000
			setv va99 0
			esee 2 13 9
				addv va99 1
			next
*if less than 4 balloon bugs in sight
			doif va99 le 10
				setv va98 totl 2 18 24
*	and less than 3 balloon bug eggs in world
				doif va98 lt 3
*		create some eggs
					inst
					setv va90 posl
					setv va91 post
					new: simp 2 18 24 "blnk" 1 0 0
					doif tmvt va90 va91 <> 1
						kill targ
						stop
					endi
					attr 16
					tick 3600
					mvto va90 va91
				endi
			endi
		endi
	endi
	targ ownr
	
*die if older than 5 to 15 minutes old
	doif ov01 ge rand 1200 3600
		gsub die
		stop
	endi

	subv ov02 1

*if need food then track down flower
	doif ov02 le rand 100 200 and ov00 ne 3 and ov00 ne 1
*	if on the floor get off it
		doif ov00 eq 1
			setv ov91 100
		endi
*	find the closest flower
		setv va00 10000
		seta va02 null
		inst
		esee 2 7 0
			setv va01 rely ownr targ
			doif va01 lt 0
				negv va01
			endi
			doif va01 lt 100
				setv va01 relx ownr targ
				doif va01 lt 0
					negv va01
				endi
				doif va01 lt va00
					setv va00 va01
					seta va02 targ
				endi
			endi
		next
		slow
		doif va02 ne null
			seta ov16 va02
			setv ov00 3
		else
			setv ov00 0
			setv ov91 0
		endi
	endi

*hunting
	doif ov00 eq 3
		setv ov14 3
		setv ov15 3
		gsub home
		gsub velo
*	see if touching flower
		inst
		doif ov16 ne null
			doif touc ov16 ownr gt 0
				mesg writ ov16 303
				setv ov00 0
				addv ov02 100
			endi
		else
			setv ov00 0
		endi
		slow
	endi



**if on ground
	doif ov00 eq 1
		addv ov91 1
*	go to in air mode
		doif ov91 ge rand 30 50
			doif ov10 lt 0
				anim [8 9 10 11 12 13 14 15 16]
				accg 0.3
			else
				anim [22 23 24 25 26 27 28 29 30]
				accg 0.3
			endi
			setv vely -10
			over
			setv ov00 0
			setv ov91 0
		endi

		doif fall eq 0
			doif ov10 lt 0
				gsub left
			else
				gsub rite
			endi
		endi

	endi


**if flying
	doif ov00 eq 0
		addv ov91 1
*	go to on ground mode			
		doif ov91 ge rand 30 50
			setv ov91 0
			setv ov00 1
			accg 3
			doif ov12 lt 0
				pose 0
			else
				pose 4
			endi
			stop


		endi
*set max velocity
		setv ov14 5
		setv ov15 5

		subv ov90 1

		gsub wall
**if not changing X direction because of walls..
		doif ov90 le 0

			doif va00 ne 1
				doif rand 0 2 eq 0
*change X direction
					negv ov10
				endi
				addv ov90 rand 1 3
			endi
**if not changing Y direction because of walls..
			doif va01 ne 1
*change Y direction
				negv ov11
				addv ov90 rand 2 5
			endi
		endi

		gsub velo
		gsub anim


	endi


******WALK LEFT on ground
	subr left
		anim [0 1 2 3]
		velo -3 -5
	retn

******WALK RIGHT on ground
	subr rite
		anim [4 5 6 7]
		velo 3 -5
	retn



********VELOCITY GIVER
	subr velo
		doif ov10 lt 0
*trying to go left
			negv ov14
			doif ov12 gt ov14
				subv ov12 2
			endi
			negv ov14
		else
*trying to go right
			doif ov12 lt ov14
				addv ov12 2
			endi
		endi

		doif ov11 lt 0
*trying to go up
			negv ov15
			doif ov13 gt ov15
				subv ov13 2
			endi
			negv ov15
		else
*trying to go down
			doif ov13 lt ov15
				addv ov13 2
			endi
		endi
**give velocity

		setv velx ov12
		setv vely ov13
	retn

********AVOID WALLS ROUTINE
	subr wall
*change direction to right
		doif obst 0 lt 100 and ov10 lt 0
			negv ov10
			setv va00 1
		endi
*change direction to left
		doif obst 1 lt 100 and ov10 gt 0
			negv ov10
			setv va00 1
		endi
*change direction to up
		doif obst 2 lt 50 and ov11 lt 0
			negv ov11
			setv va01 1
		endi
*change direction to down
		doif obst 3 lt 100 and ov11 gt 0
			negv ov11
			setv va01 1
		endi
	retn

********ANIMATION ROUTINE - flying only
	subr anim
**flying movement animation
		doif ov12 gt 0
			anim [31 32 33 34 35 255]
		elif ov12 lt 0
			anim [17 18 19 20 21 255]
		endi
	retn

***Homing in on target - alters ov10 and ov11 to aim at target (ov16)
	subr home
		inst
		targ ov16
		doif targ ne null
			setv va00 relx ownr targ


			targ ownr
			doif va00 lt 0
				doif ov10 gt 0
					negv ov10
				endi
			else
				doif ov10 lt 0
					negv ov10
				endi
			endi

			doif ov16 eq null
				setv ov00 0
				stop
			endi
			setv va00 rely ownr ov16

			targ ownr
			doif va00 lt 0
				doif ov11 gt 0
					negv ov11
				endi
			else
				doif ov11 lt 0
					negv ov11
				endi
			endi

		else
			targ ownr
			setv ov00 0
		endi
		slow
	retn

subr die
**Check direction
        doif ov12 gt 0 or ov10 gt 0
            pose 39
        elif ov12 lt 0 or ov10 lt 0
            pose 36
        else
            pose 39
        endi
        accg 1
        loop
            wait 10
        untl fall = 0
        wait rand 50 100
        doif pose eq 36
            pose 37
        else
            pose 40
        endi
        wait rand 50 100
        doif pose eq 37
            pose 38
        else
            pose 41
        endi
        wait rand 50 100
        kill ownr
retn
endm

**balloon bug egg timer
scrp 2 18 24 9
	setv va00 posl
	setv va01 post
	inst
	new: simp 2 13 9 "balloonbug" 42 0 3000
	attr 198
	clac 0
*	bhvr 59
	elas 0
*bioenergy
	setv ov61 40
	mvto va00 va01



	setv ov10 rand 0 1
	doif ov10 eq 0
		setv ov10 -1
	endi
	setv ov11 rand 0 1
	doif ov11 eq 0
		setv ov11 -1
	endi

	tick 5
	kill ownr
endm

*hit script
scrp 2 13 9 3
	snde "cmc3"
	inst
	doif from <> null
		targ from
		doif crea targ = 1
* REDUCE Pain, Fear and Anger!
			driv 0 -0.45
			driv 10 -0.35
			driv 12 -0.35
		endi
	endi
	kill ownr
endm

*eat script
scrp 2 13 9 12
	snde "chwp"
	stim writ from 80 1
	kill ownr
endm

*pull
scrp 2 13 9 2
	doif ov00 eq 0
*make the change activity counter large enough to trip
		setv ov91 100
	endi
	stim writ targ 85 1
	targ ownr
endm

******REMOVAL
rscr
enum 2 13 9
	kill targ
next
enum 2 18 24
	kill targ
next
scrx 2 13 9 9
scrx 2 13 9 1
scrx 2 13 9 2
scrx 2 13 9 3
scrx 2 13 9 12
scrx 2 18 24 9
