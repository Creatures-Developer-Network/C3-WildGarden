*GB Template
*# Pray-File "GB_Foxglove.agent"
*# DS-Name "Foxglove"
*# Depend fxgl.c16
*# Attach gb_c3_conv.c16

*# desc = "C3 Foxglove raw, buggy, unsupported conversion."
*# Agent Animation File = "gb_c3_conv.c16"
*# Agent Animation Gallery = "gb_c3_conv"
*# Agent Sprite First Image = 8
*# GB_Category = 2
*# Agent Author = "Creature Labs"


new: simp 2 3 1 "fxgl" 1 rand 25 28 200
attr 193
bhvr 48
elas 50
fric 100
accg 1


setv ov00 1

setv ov70 100
setv ov71 1000

setv ov72 50

setv ov02 100


setv ov80 1
setv ov81 0.1

setv ov82 1
setv ov83 0.01
setv ov84 0.001

setv ov85 1
setv ov86 0.1
mvsf game "ch_x" game "ch_y"
velo rand -10 10 rand -10 0
tick ov70



scrp 2 3 1 9

	doif ov00 eq 1 and carr eq null and fall eq 0


		doif rtyp room targ lt 5 or rtyp room targ eq 8 or rtyp room targ eq 9

			gsub nope
		elif prop room targ 1 gt ov80 or prop room targ 1 lt ov81

			gsub nope
		elif prop room targ 3 gt ov85 or prop room targ 3 lt ov86

			gsub nope
		elif prop room targ 2 gt ov84 and prop room targ 2 lt ov83

			gsub nope
			gsub dorm
		elif prop room targ 2 gt ov82 or prop room targ 2 lt ov83

			gsub nope
		else
			rnge 300

			setv va00 0
			esee 2 4 1
				addv va00 1
			next
			doif va00 ge 4

				gsub dorm
			else

				gsub grow
			endi
		endi
	endi


	subr grow

		inst

		setv va00 posl
		setv va01 post
		setv va02 wdth
		setv va03 hght

		new: simp 2 4 1 "fxgl" 12 0 rand 200 500



		seta ov17 null
		seta ov18 null

		setv ov30 5

		setv ov31 11

		setv ov32 6

		setv ov50 0.1

		setv ov51 0.005

		setv ov52 0.5

		setv ov53 1

		setv ov54 10

		setv ov55 0.1

		setv ov56 0.0001

		setv ov57 0.5

		setv ov58 0.001

		setv ov60 0

		setv ov61 10

		setv ov62 0.0002

		setv ov63 500

		setv ov65 1


		setv ov66 30

		setv ov67 30

		setv ov68 3

		setv ov69 0



		setv ov70 0.0001
		setv ov71 0.001
		setv ov72 0.001

		setv ov73 30
		setv ov74 30

		setv ov80 300
		setv ov82 100


		setv va04 wdth
		subv va04 va02
		divv va04 2
		setv va05 hght

		subv va05 va03
		subv va00 va04
		subv va01 va05

		mvto va00 va01

		tick ov80

		kill ownr
	retn

	subr dorm
		doif ov72 le 0
			tick ov71
		else
			tick ov70
		endi
	retn

	subr nope
		subv ov72 1
		subv ov02 1
		doif ov02 le 0
			gsub dead
		endi
	retn

	subr dead

		altr room targ 3 0.0001
		altr room targ 4 0.0001
		kill targ
	retn
endm




scrp 2 3 1 12
	lock
	stim writ from 77 1
	wait 1
	kill ownr
endm









scrp 2 6 2 300
	inst
	addv ov51 1
	doif ov51 gt 5
		doif ov00 eq 0 or ov00 eq 1
			setv ov00 0
			base ov30
			pose 0
		elif ov00 eq 2 or ov00 eq 3
			setv ov00 2
			base ov38
			pose 0
		endi
	endi
	setv ov99 1
	targ ov17
	addv ov60 1
endm


scrp 2 6 2 301

	inst
	doif _p1_ eq 0

		doif ov00 eq 0 or ov00 eq 2
			base ov30
			pose 0
			doif ov00 eq 2
				setv ov00 0
			endi
		elif ov00 eq 1 or ov00 eq 3

			base ov37
			pose 0
			doif ov00 eq 3
				setv ov00 1
			endi
		endi
	else


		doif ov00 eq 0
			base ov38
			pose 0
		elif ov00 eq 1
			base ov39
			pose 0
		endi
	endi
endm


scrp 2 6 2 302
	inst
	subv ov51 1
	setv ov99 0
	targ ov17
	subv ov60 1
endm


scrp 2 6 2 12
	inst
	doif ov99 eq 0
		stop
	endi
	subv ov51 1
	doif ov51 le 1
		setv ov99 0
	endi
	doif ov51 le 5
		doif ov00 eq 0
			setv ov00 1
			base ov37
			pose 0
		elif ov00 eq 2
			setv ov00 3
			base ov38
			pose 0
		endi
	endi
	targ ov17
	doif targ ne null
		subv ov60 1
	endi
endm




scrp 2 7 1 300
	inst
	setv va00 pose
	addv va00 1
	pose va00
	targ ov17
	addv ov69 1
endm


scrp 2 7 1 301
	inst


	doif pose le ov31
		setv va00 ov31
		setv va01 ov32
		addv va00 va01
		pose va00
		targ ov17
		subv ov69 1
	else

		setv va00 pose
		subv va00 1
		pose va00
		targ ov17
		subv ov69 1
	endi
endm


scrp 2 7 1 302
	inst

	doif pose eq ov31
		pose ov34
		targ ov17
		subv ov69 1
	else

		setv va00 pose
		addv va00 1
		pose va00
		targ ov17
		subv ov69 1
	endi

	setv va00 rand 25 28
	setv va01 plne
	subv va01 1
	setv va02 posx
	setv va03 posy
	new: simp 2 3 1 "fxgl" 1 va00 va01
	attr 195
	bhvr 48
	elas 50
	fric 100
	accg 1
	mvto va02 va03
	velo rand -10 10 rand -5 0

	setv ov00 1

	setv ov70 10
	setv ov71 100

	setv ov72 50

	setv ov02 100

	setv ov80 1
	setv ov81 0.1

	setv ov82 1
	setv ov83 0.01
	setv ov84 0.001

	setv ov85 1
	setv ov86 0.1
	tick ov70
endm


scrp 2 7 1 303

	inst
	targ ov17
	doif targ ne null
		doif ov00 eq 2
			setv ov00 4
		endi
	endi
endm






scrp 2 4 1 9




	gsub up_t

	gsub loss

	gsub what


	doif ov00 eq 0
		setv va00 ov30
		subv va00 1
		doif pose lt va00
			setv va00 pose
			addv va00 1
			pose va00
		else

			doif ov61 gt 0 and ov60 eq 0
				inst
				setv va00 plne
				addv va00 1
				setv va01 posl
				setv va02 post
				setv va03 ov60
				setv va04 ov61
				new: simp 2 6 2 "fxgl" 4 21 va00
				seta ov17 ownr
				mvto va01 va02
				seta va00 targ


				setv ov30 0

				setv ov37 1

				setv ov38 2

				setv ov39 3

				setv ov50 va04
				setv ov51 va03
				targ ownr
				seta ov17 va00
				addv ov60 1



				pose ov30
				subv ov55 ov72
			endi
			setv ov00 1
		endi

		subv ov55 ov70
	endi


	doif ov00 eq 1

		doif ov60 lt ov61

			subv ov55 ov72
			mesg writ ov17 300
		endi
		targ ownr

		subv ov66 1
		doif ov66 le 0
			setv ov00 2
		endi
	endi


	doif ov00 eq 2

		doif ov60 lt ov61

			subv ov55 ov72
			mesg writ ov17 300
		endi
		targ ownr

		doif ov68 ne 0 and ov69 eq 0

			inst

			setv va00 posl
			setv va01 post
			addv va00 35
			subv va01 38
			setv va02 plne
			addv va02 1
			new: simp 2 7 1 "fxgl" 9 12 va02
			mvto va00 va01

			setv ov30 0

			setv ov31 2

			setv ov32 3

			setv ov33 5

			setv ov34 6

			setv ov35 8

			seta va99 targ
			seta ov17 ownr

			targ ownr
			seta ov18 va99
			addv ov69 1
			subv ov55 ov71
		elif ov68 ne ov69
			mesg writ ov18 300
			subv ov55 ov71
		endi
		doif ov68 eq ov69

			subv ov67 1
		endi

		targ ov18
		setv va00 0
		doif targ ne null
			doif ov70 eq 1
				setv va00 1
			endi
		endi
		targ ownr
		doif va00 eq 1
			setv ov00 4
		endi

		doif ov67 le 0
			setv ov00 3
		endi
	endi




	doif ov00 eq 3

		doif ov60 lt ov61

			subv ov55 ov72
			mesg writ ov17 300
		endi
		targ ownr



		doif ov65 eq 1

			doif ov69 ne 0

				mesg writ ov18 301
			else




				doif rand 0 5 eq 0
					inst
					setv va02 posx
					setv va03 post
					new: simp 2 3 1 "fxgl" 1 va00 va01
					attr 195
					bhvr 48
					elas 50
					fric 100
					accg 1
					doif tmvt va02 va03 eq 1
						mvto va02 va03
					else
						mvsf va02 va03
					endi
					velo rand -10 10 rand -5 0

					setv ov00 1

					setv ov70 10
					setv ov71 100

					setv ov72 50

					setv ov02 100


					setv ov80 1
					setv ov81 0.1

					setv ov82 1
					setv ov83 0.01
					setv ov84 0.001

					setv ov85 1
					setv ov86 0.1
					tick ov70
				endi
				targ ownr

				kill ov18
				seta ov18 null


				setv ov00 5
			endi
		else

			setv ov00 4
		endi
	endi


	doif ov00 eq 4

		doif ov69 ne 0

			mesg writ ov18 302
		else

			kill ov18
			seta ov18 null
			setv ov66 ov73
			setv ov67 ov74







			targ ownr
			setv ov00 5


		endi
	endi



	doif ov00 eq 5



		tick ov82
		doif pose eq ov32
			kill targ
		endi

		doif ov69 gt 0
			mesg writ ov18 301
		else
			doif ov18 ne null
				kill ov18
				seta ov18 null
			endi

			doif ov60 gt 0
				mesg writ ov17 302
			else
				doif pose eq ov30
					inst
					kill ov17
					pose ov31
					slow
				endi
				setv va00 pose
				subv va00 1
				pose va00
			endi
		endi
	endi





	subr up_t

		setv va01 prop room targ 1

		setv va02 prop room targ 2

		setv va03 prop room targ 3

		setv va04 prop room targ 4



		doif ov50 lt ov52

			doif ov51 le va03
				setv va90 ov51
			else
				setv va90 va03
			endi

			setv va00 va90
			negv va00
			altr room targ 3 va00

			addv ov50 va90
			setv va90 0
		endi



		doif ov55 lt ov57

			doif ov56 le va04
				setv va91 ov56
			else
				setv va91 va04
			endi

			setv va00 va91
			negv va00


			divv va00 2
			altr room targ 4 va00

			doif va01 lt ov58

				divv va91 2
			endi

			addv ov55 va91
			setv va91 0
		endi


		doif ov55 lt ov57

			doif ov00 eq 1 and ov60 gt 0

				doif va01 gt ov58

					setv va91 ov62

					mulv va91 ov60

					addv ov55 va91
				endi
			endi
		endi
	retn



	subr loss

		setv va02 prop room targ 2


		setv va92 ov60

		addv va92 1

		setv va00 va02
		divv va00 ov63

		mulv va92 va00

		subv ov50 va92








		divv va92 2
		altr room targ 3 va92
	retn



	subr what

		doif ov50 le 0 or ov55 le 0



			setv ov00 5
		endi

		setv va03 ov52
		divv va03 ov54

		doif ov50 lt va03
			setv ov64 1
		else
			setv ov64 0
		endi

		doif ov60 ne 0
			mesg wrt+ ov17 301 ov64 0 0
		endi
	retn

endm

rscr
enum 2 3 1
	kill targ
next
enum 2 4 1
	kill targ
next
enum 2 6 2
	kill targ
next
enum 2 7 1
	kill targ
next
scrx 2 6 2 300
scrx 2 6 2 301
scrx 2 6 2 12
scrx 2 4 1 9
scrx 2 3 1 9
