*GB Template
*# Pray-File "GB_Gnarler.agent"
*# DS-Name "Gnarler (Modified)"
*# Depend gnarl.c16
*# Attach gb_c3_conv.c16

*# desc = "C3 Gnarler conversion. Modified to gain nutrition from all toys."
*# Agent Animation File = "gb_c3_conv.c16"
*# Agent Animation Gallery = "gb_c3_conv"
*# Agent Sprite First Image = 9
*# GB_Category = 3
*# Agent Author = "Creature Labs"

setv va99 rand 0 1
setv va98 va99
mulv va99 10
addv va99 176
new: simp 2 18 18 "gnarl" 10 va99 1000
attr 195
bhvr 32
accg 5
aero 20
elas 0
puhl -1 25 30

setv ov61 20
tick 100
setv ov10 va98

mvsf game "ch_x" game "ch_y"

scrp 2 15 22 1
	negv ov10
endm


scrp 2 15 22 9
	rnge 1000
*ov00.. 9 = dead
	doif ov00 eq 9
		gsub dead
		stop
	endi



	doif rtyp room targ eq 8 or rtyp room targ eq 9
		subv ov02 100
	endi


	doif ov00 eq 2

		doif ov05 eq 2
			setv va00 0
			inst
			esee 2 21 0
				addv va00 1
			next
			setv va01 0
			esee 2 15 22
				addv va01 1
			next
			esee 2 18 18
				addv va01 1
			next
			slow
			targ ownr

			doif va00 ge 20 and va01 lt 10

				setv ov00 3
			else

				doif ov02 le 5

					setv ov00 3
				else
					setv ov00 0
					tick 8
				endi
			endi



		else
			setv ov00 0
			tick 8
		endi
	endi


	doif ov05 eq 0
		addv ov01 1
		doif ov01 ge 100
			gsub age_
		endi
	endi



	doif ov00 eq 3
		inst
		setv va00 posl
		setv va01 post
		setv va99 rand 0 1
		setv va98 va99
		mulv va99 10
		addv va99 176
		new: simp 2 18 18 "gnarl" 10 va99 4000
		attr 195
		bhvr 32
		accg 2
		accg 5
		aero 20
		elas 0
		puhl -1 25 30
		mvto va00 va01
		setv ov61 20
		tick rand 1800 5400
		setv ov10 va98
		targ ownr
		setv ov00 0
		tick 8
	endi





	subv ov02 1

	doif ov02 le 0 and ov90 eq 0
		lock
		anim []
		doif ov10 lt 0
			base 64
			anim [0 1 2 3 4 5 6 7 8]
		else
			base 76
			anim [0 1 2 3 4 5 6 7 8]
		endi
		over
		attr 192
		setv ov00 9
		tick 600
		stop
	endi

	doif ov02 le rand 50 150
		setv ov00 1
	endi


	doif ov02 ge 150
		doif carr eq null
			doif rand 0 5 eq 0

				setv va00 0
				rtar 1 1 11
				doif targ ne null
					setv va00 ov00
				endi
				targ ownr
				doif va00 eq 0

					setv va02 216
				else
					setv va02 218
				endi
				inst
				setv va00 posx
				setv va01 posy
				subv va01 10
				new: simp 2 10 6 "graz" 2 va02 1999
				attr 192
				aero 5
				accg 5
				elas 0
				doif tmvt va00 va01 eq 1
					mvto va00 va01
				else
					mvsf va00 va01
				endi
				slow
				targ ownr
			endi
		endi
	endi







	doif ov00 eq 1
		setv va00 0
		setv va60 posx
		setv va90 ov10
		inst
		etch 2 21 0
*lick toys
			setv va61 posx

			doif va90 lt 0

				doif va60 ge va61
					setv va00 1
					seta va99 targ
					targ ownr
					frat 6
				endi
			else

				doif va60 le va61
					setv va00 1
					seta va99 targ
					targ ownr
					frat 6
				endi
			endi
		next
*slow
		frat 1

		doif va00 eq 1

			doif rand 0 6 eq 0
				setv va48 rand 0 2
				doif va48 eq 0
					sndc "gna1"
				elif va48 eq 1
					sndc "gna2"
				elif va48 eq 2
					sndc "gna3"
				endi
			endi
			doif ov10 lt 0
				base 0
				anim [24 25 26 27 28 29 30]
				over
				inst
				targ va99
				doif targ eq null
					stop
				endi
*CHECK FOR MOVABILIY FIRST~
*We'll say if it's pickupable by creatures, its pickupable by these:
				setv va00 bhvr
				andv va00 32
				doif va00 = 32
					velo -10 -45
				endi
*just for fun-- if it's pushable by creatures, trigger the push script
				setv va00 bhvr
				andv va00 1
				doif va00 = 1
					mesg writ targ 1
				endi

				targ ownr
				base 0
				anim [31 32 33 34 35]
				over

				base 0
				anim [48 49 50 51 52 53 54 55]
				over
			else
				base 0
				anim [36 37 38 39 40 41 42]
				over
				targ va99
				doif targ eq null
					stop
				endi
*CHECK FOR MOVABILIY FIRST~
*We'll say if it's pickupable by creatures, its pickupable by these:
				setv va00 bhvr
				andv va00 32
				doif va00 = 32
					velo 10 -45
				endi
*just for fun-- if it's pushable by creatures, trigger the push script
				setv va00 bhvr
				andv va00 1
				doif va00 = 1
					mesg writ targ 1
*wait 0
				endi
				targ ownr
				base 0
				anim [43 44 45 46 47]

				base 0
				anim [56 57 58 59 60 61 62 63]
				over
			endi
			setv ov00 0
			addv ov02 20
		endi
	endi






	doif room targ ne -1
		doif prop room targ 5 ge 0.00001
			setv va00 lorp room targ 5 0
			setv va01 torx va00
			doif va01 lt 0 and ov10 gt 0
				negv ov10
			elif va01 gt 0 and ov10 lt 0
				negv ov10
			endi
		endi
	endi


	doif ov10 lt 0
		setv velx -8
		base 0
		anim [0 1 2 3 4 5 6 7 255]
	else
		setv velx 8
		base 0
		anim [8 9 10 11 12 13 14 15 255]
	endi



	doif ov00 eq 0

		doif prop room targ 1 lt 0.2 or rand 0 19 eq 0
			doif ov10 lt 0
				base 0
				anim [16 17 18 19]
			else
				base 0
				anim [20 21 22 23]
			endi
			velo 0 0
			over
			tick rand 600 1800
			setv ov00 2
		endi
	endi




	subr dead
		plne 100
		altr room targ 4 0.1
		altr room targ 3 0.1
		doif pose lt 11
			setv va00 pose
			addv va00 1
			pose va00
		else
			kill ownr
		endi
	retn


	subr age_
		velo 0 0
		setv va99 ov10
		doif ov10 lt 0
			base 0
			anim [16 17 18 19]
		else
			base 0
			anim [20 21 22 23]
		endi
		over
		doif ov10 lt 0
			base 0
			anim [19 18 17 16]
		else
			base 0
			anim [23 22 21 20]
		endi
		setv vely -20
		wait 1
		inst
		setv va00 posl
		setv va01 post
		subv va00 16
		subv va01 10

		new: simp 2 15 22 "gnarl" 88 0 4100
		attr 198
		perm 80

		doif tmvt va00 va01 ne 1
			kill targ
			targ ownr
			subv ov01 10
			stop
		endi

		clac 0
		fric 10
		elas 30
		accg 2
		setv ov05 2
		mvto va00 va01
		tick 8
		setv ov10 va99

		setv ov02 rand 10 20
		kill ownr
	retn


endm


scrp 2 15 22 6

	doif wall eq left or wall eq rght
		negv ov10
	endi
endm


scrp 2 10 6 6
	pose 1
	tick 400
endm

scrp 2 10 6 9

	altr room targ 3 0.1
	altr room targ 4 0.3

	kill ownr
endm


scrp 2 18 18 9
	doif carr ne null
		tick 300
		stop
	endi
	doif pose eq 9
		kill ownr
	endi
	base 0
	anim [0 1 2 3 4 5 6 7 8]
	over
	inst
	attr 0

	doif ov10 eq 0
		setv va99 -1
	else
		setv va99 1
	endi
	setv va00 posl
	setv va01 post
	new: simp 2 15 22 "gnarl" 88 88 4100
	attr 198
	clac 0
	fric 10
	elas 30
	perm 80
	accg 2
	setv ov05 0
	tick 8
	doif tmvt va00 va01 eq 1
		mvto va00 va01
	else
		mvsf va00 va01
	endi

	setv ov02 rand 20 50

	setv ov61 75
	setv ov10 va99

	targ ownr
	pose 9
	wait 300
	kill ownr
endm


rscr
enum 2 15 22
	kill targ
next
enum 2 18 18
	kill targ
next
enum 1 1 9
	kill targ
next
scrx 2 15 22 9
scrx 2 15 22 6
scrx 2 18 18 9
