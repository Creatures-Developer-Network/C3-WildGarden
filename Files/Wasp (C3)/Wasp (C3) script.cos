*GB Template
*# Pray-File "GB_Wasp.agent"
*# DS-Name "Wasp (C3)"
*# Depend wasp.c16
*# Attach gb_c3_conv2.c16

*# desc = "C3 Wasp raw, buggy, unsupported conversion."
*# Agent Animation File = "gb_c3_conv2.c16"
*# Agent Animation Gallery = "gb_c3_conv2"
*# Agent Sprite First Image = 16
*# GB_Category = 3
*# Agent Author = "Creature Labs"

*===========================================================
* AGENT NOTES â€“ GB Wasp (C3 Conversion)
*===========================================================
* Overview:
* - This is a raw, buggy, unsupported conversion of the C3 wasp agent.
* - Behaviours include roaming, hunting food, retaliating when pushed, and dying/rotting.
* - Some variables and routines are unused or redundant.

* Object Variables (ovXX):
* - ov00 = State (0 = roam, 1 = seek food, 99 = die)
* - ov01 = Age counter (used in death routine)
* - ov02 = Hunger/energy level (decreases over time, increased when eating)
* - ov10/ov11 = Movement direction (X/Y)
* - ov16 = Current target (food/enemy)
* - ov17 = Attacker/pusher reference
* - ov61 = Bioenergy (life force, corpse state)
* - ov72 = Food energy gain (+400 when eating)
* - ov73 = Hunger threshold (400)
* - ov74 = Fullness threshold (800)
* - ov88 = Attack timer (counts down from 80)
* - UNUSED: ov05 (life stage), ov06 (gender), ov20 (sex drive), ov75 (food flag), ov89 (aggression flag)

* Subroutines:
* - Active: die_, gfod (get food), atak (attack), roam, obst (obstacle check),
*           vect (vector choice), anim (animation), move, find (search), hunt (chase target)
* - Unused: swrm (swarming behavior, never called), near (commented out in attack)

* Known Redundancies / Issues:
* - Duplicate death check in timer script.
* - ov05, ov06, ov20, ov75, ov89 are initialized but never used.
* - swrm and near subroutines are defined/commented but not integrated.
* - Corpse script checks carr = null twice (redundant).
* - Attack routine has commented-out feeding logic.
* - Obstacle routine has commented-out floor detection.

*===========================================================


new: simp 2 14 6 "wasp" 221 0 300
perm 100
tick 6
attr 199
aero 5
accg 0
clac 0
elas 10
bhvr 17
setv ov00 0
setv ov01 2000
setv ov02 800ple
*setv ov05 2
*setv ov06 rand 0 1
setv ov10 1
setv ov11 -1
seta ov16 null
seta ov17 null
*usually sex drive, but no mating behaviour is coded
*setv ov20 0
setv ov61 45
setv ov72 400
setv ov73 400
setv ov74 800
*setv ov75 1
anim [5 7]
mvsf game "ch_x" game "ch_y"
setv vely 0

**Wasp Eat Script
scrp 2 14 6 12
	stim writ from 80 1
	kill ownr
endm

**Wasp Push Script
scrp 2 14 6 1
	seta ov17 from
	setv ov88 80
*	setv ov89 1
	stim writ from 88 1
	targ ownr

endm

*Wasp timer script
scrp 2 14 6 9
*metabolism
	addv ov01 1
	subv ov02 1
*	addv ov20 1

*if hungry set state to get food
	doif ov02 < ov73
		setv ov00 1
	endi
***I'm not sure why this is repeated?
	doif ov00 eq 99
*gsub die_
	endi

***Die!
	doif ov00 eq 99
		gsub die_
	endi

**Someone pushed you?
	doif ov17 <> null
		doif ov88 > 0
			subv ov88 1
			doif ov88 > 40
**Attack!
				gsub atak
			endi
			doif ov88 = 0
				seta ov17 null
			endi
		endi
	endi

***STATE CHECKING***
***ov00 should be the state
** 0 = Roaming
** 1 = Looking for Food
** 2 Unused but in a comment: possibly intended to be 'if well-fed, seek a mate'.
** 99 = Die!
	doif ov00 = 1
		gsub gfod
	endi

*If state is 0, roam
	doif ov00 eq 0
		gsub roam
	endi

***SUBROUTINES****
*Die
	subr die_
		setv va50 posx
		setv va51 posy
		setv va52 ov10
		subv va50 20
		subv va51 8
		doif ov01 gt 2000
			new: simp 2 10 28 "wasp" 1 0 2000
		else
			new: simp 2 10 28 "wasp" 1 1 2000
		endi
		accg 2

		tick 4
		attr 195
		aero 0
		setv ov10 va52
		setv ov01 0
		doif tmvt va50 va51 <> 1
			kill targ
			kill ownr
			stop
		endi
		mvto va50 va51
		setv ov61 18
		setv vely 0
		targ ownr
		kill targ
	retn

*Get food
	subr gfod
		inst
		setv va47 2
		setv va48 8
		setv va49 0
		doif ov16 = null
			gsub find
		endi
		doif ov16 = null
			setv va47 2
			setv va48 9
			setv va49 0
			gsub find
		endi
		doif ov16 <> null
			targ ownr
			gsub hunt
			doif touc ownr ov16 <> 0
				targ ownr
				addv ov02 ov72
				mesg writ ov16 12
				doif ov02 gt ov74
					setv ov00 0
				endi
			endi
		else
			targ ownr
			setv ov00 0
			gsub roam
		endi
		targ ownr
		gsub vect
		gsub anim
		gsub move
		stop
	retn

*attack
	subr atak
		snde "wasp"
		inst
		seta va17 ov17
		esee 2 14 6
			doif ov17 = null and targ <> ownr and ov88 = 0 and targ <> null
				seta ov17 va17
				setv ov88 80
			endi
		next
		seta ov16 ov17
		doif ov16 <> null
			targ ownr
			gsub hunt
*			gsub near
*note 'near' is not included in the code: this was probably intended to check nearness before attacking but the subroutine is missing
*			doif va40 eq 1
			doif touc ownr ov16 <> 0
*				targ ownr
*				addv ov02 ov72
*				mesg writ ov16 12
*				doif ov02 gt ov74
*					setv ov00 2
*				endi
				targ ov16
				doif fmly = 4
					stim writ targ 88 1
					targ ownr
				endi
			else
				targ ownr
				setv ov00 0
				gsub roam
			endi
			targ ownr
			gsub vect
			gsub anim
			gsub move
			stop
		endi
	retn

*Swarm - never used
	subr swrm
		setv va88 0
		esee 2 14 6
			doif targ <> ownr and targ <> null
				setv va88 1
				setv va50 relx ownr targ
				setv va51 rely ownr targ
				addv va20 va50
				addv va21 va51
			endi
		next
		doif va88 = 1
			doif va20 <= 0
				setv ov10 -1
			endi
			doif va20 > 0
				setv ov10 1
			endi
			doif va21 <= 0
				setv ov11 -1
			endi
			doif va21 > 0
				setv ov11 1
			endi

		endi
		gsub vect
		gsub anim
		gsub move
		stop
	retn

*roam randomly
	subr roam
		setv va66 rand 0 12
		doif va66 lt 5
			doif obst down > 5
				snde "wasp"
			endi
			doif va66 = 1
				setv ov10 1
			elif va66 = 2
				setv ov10 -1
			elif va66 = 3
				setv ov11 1
			elif va66 = 4
				setv ov11 -1
			endi
		endi


		gsub obst
		gsub vect
		gsub anim
		gsub move
		stop
	retn

*obstacle checking
	subr obst
***Assuming this is left, right, up, and down
**Might be wise to change this to check the LEFT, RGHT, _UP_, and DOWN looking at how this differs from how obst works in DS
		doif obst 0 lt 120
			setv ov10 1
		endi
		doif obst 1 lt 120
			setv ov10 -1
		endi

		doif obst 2 lt 60
			setv ov11 1
		endi
*** Can't detect being close to the floor? - note that wasps often travel along the floor
*		doif obst 3 lt 100
*			setv ov11 -1
*		endi
	retn

*Choose a vector
	subr vect
		setv va10 rand 8 14
		setv va11 rand 4 8
		mulv va10 ov10
		mulv va11 ov11
		doif ov12 < va10
			addv ov12 4
		elif ov12 > va10
			subv ov12 4
		endi
		doif ov13 < va11
			addv ov13 4
		elif ov13 > va11
			subv ov13 4
		endi
		setv va10 ov12
		setv va11 ov13

	retn

*animate
	subr anim
		doif ov12 eq 0
			loop
				setv ov12 rand -1 1
			untl ov12 ne 0
		endi

		doif obst down > 0
			doif ov12 <= 0
				anim [18 19 255]
			endi

			doif ov12 > 0
				anim [20 21 255]
			endi
		endi


		doif obst down <= 0
			doif ov12 <= 0
				anim [0 1 2 3 4 5 6 7 8]
			else
				anim [9 10 11 12 13 14 15 16 17]
			endi
		endi
	retn

*movement - apply velocity
	subr move

		setv velx va10
		setv vely va11
	retn

*find subroutine - pythagorean searching
	subr find
		inst
		setv va99 99999999
		seta va58 null
		inst
		esee va47 va48 va49
			doif targ <> null
				setv va88 0
				setv va50 relx ownr targ
				setv va51 rely ownr targ
				doif va50 lt 0
					negv va50
				endi
				doif va51 lt 0
					negv va51
				endi
				setv va52 va50
				mulv va52 va52
				setv va53 va51
				mulv va53 va53
				setv va54 va52
				addv va54 va53
				doif va54 lt va99
					seta va58 targ
					setv va99 va54
				endi
			endi
		next
		doif va58 <> null
			targ ownr
			seta ov16 va58
		else
			targ ownr
			seta ov16 null
		endi
		targ ownr
		slow
	retn

*hunt subroutine - get closer to target
	subr hunt
		inst
		targ ov16
		doif targ <> null
			setv va50 posx
			setv va51 posy
			targ ownr
			doif posx lt va50
				setv ov10 1
			endi
			doif posx gt va50
				setv ov10 -1
			endi
			doif posy lt va51
				setv ov11 1
			endi
			doif posy gt va51
				setv ov11 -1
			endi
		else
			targ ownr
			seta ov16 null
			setv ov00 0
		endi
	retn

*end wasp timer script
endm


***Bumped into a Wall!
scrp 2 14 6 6
	inst
	setv ov12 0
	setv ov13 0
endm


***Rotting Dead Wasp
scrp 2 10 28 9
	inst
	doif carr = null
		doif room targ <> -1 and carr = null
			altr room targ 3 0.1
			altr room targ 4 0.1
		endi

		kill targ
	endi
endm


***REMOVAL

rscr

enum 2 14 6
	kill targ
next
scrx 2 14 6 9
scrx 2 14 6 6
scrx 2 14 6 1

enum 2 10 28
	kill targ
next
scrx 2 10 28 9
scrx 2 10 28 6

endm
